allprojects {
    group = 'de.scholle'
    version = '1.1.0'

    repositories {
        mavenCentral()
        maven {
            name = 'Spigot Legacy'
            url = 'https://hub.spigotmc.org/nexus/content/repositories/public/'
        }
        maven {
            name = 'Paper'
            url = 'https://repo.papermc.io/repository/maven-public/'
        }
        maven {
            name = 'Sonatype OSS'
            url = 'https://oss.sonatype.org/content/repositories/snapshots'
        }
    }
}

subprojects {
    apply plugin: 'java'

    def targetJavaVersion = 8
    java {
        def javaVersion = JavaVersion.toVersion(targetJavaVersion)
        sourceCompatibility = javaVersion
        targetCompatibility = javaVersion
        if (JavaVersion.current() < javaVersion) {
            toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'

        if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
            options.release.set(targetJavaVersion)
        }
    }

    tasks.withType(ProcessResources).configureEach {
        def props = [version: version]
        inputs.properties props
        filteringCharset = 'UTF-8'
        filesMatching(['plugin.yml', 'bungee.yml', 'velocity-plugin.json']) {
            expand props
        }
    }
}
